apply plugin: "eclipse"
apply plugin: "java"
apply plugin: "signing"
apply plugin: "maven"

group = "com.github.diegopacheco"
archivesBaseName = "rxnetty-rest-router"
version = "0.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.java.srcDirs = ["src/main/java"]
}

repositories {
  maven { url 'http://repo.spring.io/libs-milestone' }
  maven { url 'http://repo.spring.io/libs-release' }
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
	compile([
		'io.reactivex:rxnetty:0.5.1',
		
		'com.google.inject:guice:4.1.0',
		
		'org.glassfish.jersey.core:jersey-server:2.7',
		'org.glassfish.jersey.containers:jersey-container-servlet-core:2.7',
		'org.glassfish.jersey.containers:jersey-container-jetty-http:2.7',
		
		'org.slf4j:slf4j-log4j12:1.7.25',
    ])
    testCompile([
	       'junit:junit:4.12',
    ])
}

eclipse {
    classpath {
       downloadSources=true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0"
    }
}

apply plugin: 'io.codearte.nexus-staging'

signing {
     sign configurations.archives
}

uploadArchives {
     repositories {
         mavenDeployer {
             beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

             repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
             }
             snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
             }

             pom.project {
                 name project.name
                 description project.description
                 packaging 'jar'
                 url 'https://github.com/diegopacheco/rxnetty-rest-router'
                 scm {
                     connection 'scm:git:https://github.com/diegopacheco/rxnetty-rest-router.git'
                     developerConnection 'scm:git:git@github.com:diegopacheco/rxnetty-rest-router.git'
                     url 'https://github.com/diegopacheco/rxnetty-rest-router.git'
                 }
                 licenses {
                     license {
                         name 'The Unlicense'
                         url 'http://unlicense.org/'
                         distribution 'repo'
                     }
                 }
                 developers {
                     developer {
                         id = 'diegopacheco'
                         name = 'Diego Pacheco'
                         email = 'diego.pacheco.it@mail.com'
                     }
                 }
             }
         }
     }
}

nexusStaging {
  username = "${nexusUsername}"
  password = "${nexusPassword}"
  packageGroup = "${group}"
}

task generatePom {
    pom {
        project {
            name project.name
            description project.description
            groupId group
            artifactId archivesBaseName
            version version
            packaging 'jar'
            url 'https://github.com/diegopacheco/rxnetty-rest-router'
            scm {
                connection 'scm:git:https://github.com/diegopacheco/rxnetty-rest-router.git'
                developerConnection 'scm:git:git@github.com:diegopacheco/rxnetty-rest-router.git'
                url 'https://github.com/diegopacheco/rxnetty-rest-router.git'
            }
            licenses {
                license {
                    name 'The Unlicense'
                    url 'http://unlicense.org/'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id = 'diegopacheco'
                    name = 'Diego Pacheco'
                    email = 'diego.pacheco.it@mail.com'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}
